plugins {
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

repositories {
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url 'https://repo.codemc.org/repository/maven-public/' }
    maven { url 'https://repo.codemc.org/repository/nms/' }
}

dependencies {
    implementation project(':api')
    implementation project(':nms')
    implementation project(':v1_8_R3')
    implementation project(':v1_11_R1')
    implementation project(':v1_14_R1')
    implementation project(':v1_17_R1')
    implementation project(':v1_18_R1')
    implementation project(':v1_19_R2')
    compileOnly 'org.spigotmc:spigot:1.19-R0.1-SNAPSHOT'
    implementation 'com.zaxxer:HikariCP:3.4.5'
    implementation 'org.mariadb.jdbc:mariadb-java-client:2.6.2'
    implementation 'de.tr7zw:item-nbt-api:2.11.2'
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    from(sourceSets.main.resources.srcDirs) {
        include 'plugin.yml'
        expand(rootProject.properties)
    }
}

shadowJar {
    archiveClassifier.set('')

    relocate 'de.tr7zw.changeme.nbtapi', project.group + '.lib.nbt'
    relocate 'com.zaxxer.hikari', project.group + '.lib.hikari'
    relocate 'org.mariadb.jdbc', project.group + '.lib.mariadb'
    relocate 'org.javatuples', project.group + '.lib.javatuples'
}

jar {
    enabled = false
    dependsOn (shadowJar)
}